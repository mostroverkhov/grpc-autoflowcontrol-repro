/*
 * Copyright 2020 - present Maksym Ostroverkhov.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


plugins {
    id "io.spring.dependency-management" apply false
    id "com.github.sherter.google-java-format" apply false
    id "com.google.osdetector" apply false
    id "com.google.protobuf" apply false
}

apply from: "gradle/dependency-management.gradle"

allprojects {
    dependencyLocking {
        lockAllConfigurations()
    }
}

subprojects {
    apply plugin: "com.github.sherter.google-java-format"

    repositories {
        mavenCentral()
    }

    plugins.withType(JavaPlugin) {
        compileJava {
            sourceCompatibility = 1.8
            targetCompatibility = 1.8

            options.compilerArgs << "-XDignore.symbol.file"
            dependsOn "googleJavaFormat"
        }
    }

    plugins.withType(JavaLibraryPlugin) {

        task sourcesJar(type: Jar) {
            classifier "sources"
            from sourceSets.main.allJava
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier "javadoc"
            from javadoc.destinationDir
        }

        artifacts {
            archives sourcesJar
            archives javadocJar
        }
    }

    googleJavaFormat {
        toolVersion = "1.6"
        exclude "**/generated"
    }
}

defaultTasks "clean", "build"